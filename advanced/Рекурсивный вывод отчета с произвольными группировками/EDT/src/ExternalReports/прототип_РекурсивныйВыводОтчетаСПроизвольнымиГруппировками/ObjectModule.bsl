Перем Макет;

Функция СформироватьОтчет(Результат,СтруктураПараметров) Экспорт
	ДанныеОтчета = ПолучитьДанные(СтруктураПараметров);
	//вывод
	Результат.Очистить();
	Область = Макет.ПолучитьОбласть("Шапка|Начало");
	Область.Параметры.ТекстЗаголовка = "Рекурсивный вывод отчета с произвольными группировками (прототип)";
	Результат.Вывести(Область);
	Область = Макет.ПолучитьОбласть("Шапка|Месяц");
	Результат.Присоединить(Область);
	СтруктураНастроекВыводаРаздела = Новый Структура;
	СтруктураНастроекВыводаРаздела.Вставить("НазваниеРаздела","Отчет");
	СтруктураНастроекВыводаРаздела.Вставить("ОбластьСЦветом","Группировка");
	СтруктураНастроекВыводаРаздела.Вставить("УчестьЗначение2",Ложь);
	СтруктураНастроекВыводаРаздела.Вставить("УчестьЗначение2Проценты",Ложь);
	СтруктураНастроекВыводаРаздела.Вставить("ОтключитьРасшифровку",Ложь);
	ОтборРасшифровки = Новый Структура();
	ТабГруппировок = Новый ТаблицаЗначений;
	ТабГруппировок.Колонки.Добавить("ИмяГруппировки");
	ТабГруппировок.Колонки.Добавить("ПредставлениеГруппировки");
	Для Каждого Стр Из СтруктураПараметров.ТаблицаГруппировок Цикл
		Если Не Стр.Выводить Тогда Продолжить; КонецЕсли;
		НовСтр = ТабГруппировок.Добавить();
		НовСтр.ИмяГруппировки = Стр.ИмяСтроки;
		НовСтр.ПредставлениеГруппировки = Стр.ПредставлениеСтроки;
	КонецЦикла;
	ВывестиГруппуСтрок(Результат,СтруктураПараметров,СтруктураНастроекВыводаРаздела,ДанныеОтчета,ТабГруппировок,1,Новый Структура(),Истина);
	//ИТОГО
	СтруктураНастроекВыводаСтроки = Новый Структура;
	СтруктураНастроекВыводаСтроки.Вставить("НазваниеПодРаздела","ИТОГО:");
	СтруктураНастроекВыводаСтроки.Вставить("ЗаголовокВРасшифровке","");
	СтруктураНастроекВыводаСтроки.Вставить("ПустоеЗначение",Ложь);
	СтруктураНастроекВыводаСтроки.Вставить("ИмяГруппыМакета","Итого");
	СтруктураНастроекВыводаСтроки.Вставить("ОтборРасшифровки",Новый Структура);
	СтруктураНастроекВыводаСтроки.Вставить("Отступ",0);
	ВывестиСтроку(Результат,СтруктураПараметров,СтруктураНастроекВыводаРаздела,СтруктураНастроекВыводаСтроки,ДанныеОтчета);
	//ширина колонок	
	МаксДлина = СтрДлина("0,00");
	МаксСтрока = "0,00";
	Для Сч = 6 По Результат.ВысотаТаблицы Цикл
		Если МаксДлина < СтрДлина(Результат.Область(Сч,1,Сч,1).Текст) Тогда
			МаксДлина = СтрДлина(Результат.Область(Сч,1,Сч,1).Текст)+Результат.Область(Сч,1,Сч,1).Отступ;
			МаксСтрока = Результат.Область(Сч,1,Сч,1).Текст+Лев("____________________",Результат.Область(Сч,1,Сч,1).Отступ);
		КонецЕсли;
	КонецЦикла;
	ТекШирина = 12;
	ВремТабДок = Новый ТабличныйДокумент;
	ВремТабДок.Вывести(Макет.ПолучитьОбласть("ПроверкаВывода2"));
	Пока Не ТекстУмещаетсяВЯчейке(ВремТабДок.Область(1,1,1,1), ВремТабДок, МаксСтрока)Цикл
		ТекШирина = ТекШирина + 1;
		ВремТабДок.Область("C1").ШиринаКолонки = ТекШирина;
		Если ТекШирина = 100 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Результат.Область("C1").ШиринаКолонки = ТекШирина;
	Для Сч2 = 2 По Результат.ШиринаТаблицы Цикл
		МаксДлина = СтрДлина("0,00");
		МаксСтрока = "0,00";
		Для Сч = 6 По Результат.ВысотаТаблицы Цикл
			Если МаксДлина < СтрДлина(Результат.Область(Сч,Сч2,Сч,Сч2).Текст) Тогда
				МаксДлина = СтрДлина(Результат.Область(Сч,Сч2,Сч,Сч2).Текст)+Результат.Область(Сч,Сч2,Сч,Сч2).Отступ;
				МаксСтрока = Результат.Область(Сч,Сч2,Сч,Сч2).Текст+Лев("____________________",Результат.Область(Сч,Сч2,Сч,Сч2).Отступ);
			КонецЕсли;
		КонецЦикла;
		ТекШирина = 12;
		ВремТабДок = Новый ТабличныйДокумент;
		ВремТабДок.Вывести(Макет.ПолучитьОбласть("ПроверкаВывода"));
		Пока Не ТекстУмещаетсяВЯчейке(ВремТабДок.Область(1,1,1,1),ВремТабДок,МаксСтрока)Цикл
			ТекШирина = ТекШирина + 1;
			ВремТабДок.Область("C1").ШиринаКолонки = ТекШирина;
			Если ТекШирина = 40 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Результат.Область("C"+Формат(Сч2,"ЧГ=")).ШиринаКолонки = ТекШирина;
	КонецЦикла;
	//Сворачивание
	КоличествоУровнейГруппировокСтрок = Результат.КоличествоУровнейГруппировокСтрок();
	Для Сч = 1 По КоличествоУровнейГруппировокСтрок Цикл
		Результат.ПоказатьУровеньГруппировокСтрок(КоличествоУровнейГруппировокСтрок-Сч);
	КонецЦикла;
	Результат.ФиксацияСлева = 1;
	Результат.ФиксацияСверху = 5;
	Возврат Новый Структура("ДанныеОтчета",Новый ХранилищеЗначения(ДанныеОтчета, Новый СжатиеДанных(9)));
КонецФункции

Функция ВывестиСтроку(Результат,СтруктураПараметров,СтруктураНастроекВыводаРаздела,СтруктураНастроекВыводаСтроки,Таб)
	СуммыНеПустые = Ложь;
	ОтборРасшифровки = СкопироватьСтруктуру(СтруктураНастроекВыводаСтроки.ОтборРасшифровки);
	
	ИмяОбласти = ?(СтруктураНастроекВыводаСтроки.ИмяГруппыМакета="",
		СтруктураНастроекВыводаРаздела.ОбластьСЦветом,СтруктураНастроекВыводаРаздела.ОбластьСЦветом+"_"+СтруктураНастроекВыводаСтроки.ИмяГруппыМакета);
	Область = Макет.ПолучитьОбласть(ИмяОбласти+"|Начало");
	Область.Параметры[СтруктураНастроекВыводаСтроки.ИмяГруппыМакета] = СтруктураНастроекВыводаСтроки.НазваниеПодРаздела;
	Область.Область(1,1,1,1).Отступ = СтруктураНастроекВыводаСтроки.Отступ;
	Результат.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбласти+"|Месяц");
	Значение = Таб.Итог("Значение");
	Область.Параметры.Значение = Значение;
	Если Не СтруктураНастроекВыводаРаздела.ОтключитьРасшифровку Тогда
		Область.Параметры.Значение_СтруктураРасшифровки = Новый Структура("Отбор,Колонка",
			ОтборРасшифровки,"Значение");
	КонецЕсли;
	Результат.Присоединить(Область);
	Возврат Истина;
КонецФункции

Процедура ВывестиГруппуСтрок(ТабДок,СтруктураПараметров,СтруктураНастроекВыводаРаздела,Выборка,ТабГруппировок,НомерГруппировки,ОтборРасшифровки,Группировать)
	ИмяГруппировки = ТабГруппировок.Получить(НомерГруппировки-1).ИмяГруппировки;
	ПредставлениеГруппировки = ТабГруппировок.Получить(НомерГруппировки-1).ПредставлениеГруппировки;
	ЗапомнитьНомерСтроки = ТабДок.ВысотаТаблицы;
	ТекЗначенияВыборки = Выборка.Скопировать();
	ТекЗначенияВыборки.Свернуть(ИмяГруппировки);
	ТекЗначенияВыборки.Сортировать(ИмяГруппировки);
	Для Каждого Стр Из ТекЗначенияВыборки Цикл 
		НовОтборРасшифровки = СкопироватьСтруктуру(ОтборРасшифровки);
		НовОтборРасшифровки.Вставить(СтрЗаменить(ИмяГруппировки,"_Ниже",""),Стр[СтрЗаменить(ИмяГруппировки,"_Ниже","")]);
		СтруктураНастроекВыводаСтроки = Новый Структура;
		СтруктураНастроекВыводаСтроки.Вставить("НазваниеПодРаздела",Стр[ИмяГруппировки]);
		СтруктураНастроекВыводаСтроки.Вставить("ЗаголовокВРасшифровке","");
		СтруктураНастроекВыводаСтроки.Вставить("ПустоеЗначение",ИмяГруппировки = "КомментарийСтроительства");
		СтруктураНастроекВыводаСтроки.Вставить("ИмяГруппыМакета",ИмяГруппировки);
		СтруктураНастроекВыводаСтроки.Вставить("ОтборРасшифровки",НовОтборРасшифровки);
		СтруктураНастроекВыводаСтроки.Вставить("Отступ",2*(НомерГруппировки-1));
		
		ТекВыборка = Выборка.Скопировать(Новый Структура(ИмяГруппировки,Стр[ИмяГруппировки]));
		СтрокаУспешноВыведена = ВывестиСтроку(ТабДок,СтруктураПараметров,СтруктураНастроекВыводаРаздела,СтруктураНастроекВыводаСтроки,ТекВыборка);
		Если СтрокаУспешноВыведена И ТабГруппировок.Количество() >= НомерГруппировки + 1 Тогда
			ВывестиГруппуСтрок(ТабДок,СтруктураПараметров,СтруктураНастроекВыводаРаздела,ТекВыборка,ТабГруппировок,НомерГруппировки+1,НовОтборРасшифровки,Истина);
		КонецЕсли;
	КонецЦикла;
	Если Группировать Тогда
		Если НомерГруппировки > 1 И ЗапомнитьНомерСтроки > 0 И ТабДок.ВысотаТаблицы - ЗапомнитьНомерСтроки > 0 Тогда
			ТабДок.Область(ЗапомнитьНомерСтроки+1,,ТабДок.ВысотаТаблицы,).Сгруппировать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьДанные(СтруктураПараметров)
	ДанныеОтчета = Новый ТаблицаЗначений;
	ДанныеОтчета.Колонки.Добавить("Измерение1",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(25,ДопустимаяДлина.Переменная)));
	ДанныеОтчета.Колонки.Добавить("Измерение2",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(25,ДопустимаяДлина.Переменная)));
	ДанныеОтчета.Колонки.Добавить("Измерение3",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(25,ДопустимаяДлина.Переменная)));
	ДанныеОтчета.Колонки.Добавить("Значение",Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,3)));
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "красные";
	НовСтрока.Измерение2 = "яблоки";
	НовСтрока.Измерение3 = "зрелые";
	НовСтрока.Значение = 100;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "красные";
	НовСтрока.Измерение2 = "груши";
	НовСтрока.Измерение3 = "зрелые";
	НовСтрока.Значение = 66;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "зеленые";
	НовСтрока.Измерение2 = "яблоки";
	НовСтрока.Измерение3 = "зрелые";
	НовСтрока.Значение = 100;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "зеленые";
	НовСтрока.Измерение2 = "яблоки";
	НовСтрока.Измерение3 = "незрелые";
	НовСтрока.Значение = 11;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "красные";
	НовСтрока.Измерение2 = "яблоки";
	НовСтрока.Измерение3 = "зрелые";
	НовСтрока.Значение = 100;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "желтые";
	НовСтрока.Измерение2 = "яблоки";
	НовСтрока.Измерение3 = "незрелые";
	НовСтрока.Значение = 200;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "красные";
	НовСтрока.Измерение2 = "груши";
	НовСтрока.Измерение3 = "незрелые";
	НовСтрока.Значение = 19;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "зеленые";
	НовСтрока.Измерение2 = "груши";
	НовСтрока.Измерение3 = "зрелые";
	НовСтрока.Значение = 15;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "желтые";
	НовСтрока.Измерение2 = "яблоки";
	НовСтрока.Измерение3 = "зрелые";
	НовСтрока.Значение = 45;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "красные";
	НовСтрока.Измерение2 = "яблоки";
	НовСтрока.Измерение3 = "незрелые";
	НовСтрока.Значение = 12;
	НовСтрока = ДанныеОтчета.Добавить();
	НовСтрока.Измерение1 = "зеленые";
	НовСтрока.Измерение2 = "груши";
	НовСтрока.Измерение3 = "зрелые";
	НовСтрока.Значение = 100;
	Возврат ДанныеОтчета;
КонецФункции

Функция СкопироватьСтруктуру(СтруктураВход)
	СтруктураВыход = Новый Структура;
	Для Каждого Стр Из СтруктураВход Цикл
		СтруктураВыход.Вставить(Стр.Ключ,Стр.Значение);
	КонецЦикла;
	Возврат СтруктураВыход;
КонецФункции

Функция ТекстУмещаетсяВЯчейке(пОбласть,пТабДок,пТекст)     
    ВысотаДо = ВысотаОбластиВмм(пОбласть,пТабДок);    
    ВремТабДок = Новый ТабличныйДокумент;
    ВремТабДок.Вывести(пТабДок);
    ВремОбласть = ВремТабДок.Область(1,1,1,1);    
    ВремОбласть.АвтоВысотаСтроки = Истина;
    ВремОбласть.ВысотаСтроки     = 0;    
    ВремОбласть.Текст = пТекст;        
    ВысотаПосле = ВысотаОбластиВмм(ВремОбласть,ВремТабДок);
	Если ВысотаДо >= ВысотаПосле Тогда
		Возврат(Истина);
    Иначе
        Возврат(Ложь);
    КонецЕсли;
КонецФункции

Функция ВысотаОбластиВмм(пОбласть,пТабДок)
    Надпись = пТабДок.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Текст); 
    Надпись.Расположить(пОбласть);    
    Высота = Надпись.Высота;    
    пТабДок.Рисунки.Удалить(Надпись);
    Возврат(Высота);
КонецФункции

Макет = ПолучитьМакет("Макет");
